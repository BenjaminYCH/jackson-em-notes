% run
% pdflatex total-reflection.tex
% asy total-reflection-1.asy
% pdflatex total-reflection.tex
% to get the output pdf file, then open it with acrobat reader to view animation.
\documentclass{article}
\usepackage[charter]{mathdesign}
\usepackage[inline]{asymptote}
\begin{document}
 
\begin{center} 
\begin{asy}
settings.render = 0;
import three;
import animate;

animation anime = animation("total-reflection");

currentprojection = perspective((-.5,-.4,.3), up=Z);

real n = 1.50;
real nn = 1;
real iangle = radians(90);

int xsamples = 120;
int xstart = 20;
real DE = 12;
real xstep = DE / xsamples;
real lambda = 6;
real kr = 2 * pi / lambda;
real si = sin(iangle);
real ezamp = n / nn * si;
real examp = sqrt(n * n * si * si / nn / nn - 1);
real bamp = .6;
int frames = 30;
int tailNodes = 18;

triple[] ende = new triple[];
triple[] endb = new triple[];
for (int f = 0; f < frames; ++f) {
  real omegat = f * 2  pi / frames;
  triple epos = (examp * sin(-omegat), 0, ezamp * cos(-omegat));
  ende.push(epos);
  triple bpos = (0, -bamp * cos(-omegat), 0);
  endb.push(bpos);
}

int frames = 30;
for (int f = 0; f < frames; ++f) {
  real omegat = f * 2 * pi / frames;
  picture pic;
  size(pic, 8cm, 8cm);
  unitsize(pic, 2cm);

  draw(pic, O--2Z, mediumgray, arrow=Arrow3(emissive(mediumgray)));
  draw(pic, O--2Y, mediumgray, arrow=Arrow3(emissive(mediumgray)));
  draw(pic, O--15X, mediumgray, arrow=Arrow3(emissive(mediumgray)));

  path3 ge, gb;
  for (int i = 0; i <= xsamples; ++i) {
    real x = xstep * i;
    real ez = ezamp * cos(kr * x - omegat);
    real ex = examp * sin(kr * x - omegat);
    triple epos = (x + ex, 0, ez);
    triple bpos = (x, -bamp * cos(kr * x - omegat), 0);
    ge = ge -- epos;
    gb = gb -- bpos;
    if (i >= xstart) {
      draw(pic, (x, 0, 0) -- epos, red);
      draw(pic, (x, 0, 0) -- (bpos), blue);
    }
    if (i == 0) {
      for (int t = 0; t <= tailNodes; ++t) {
        real linewidth = 1-.5*t/tailNodes;
        pen pene = rgb(1, t/tailNodes, t/tailNodes);
        pen penb = rgb(t/tailNodes, t/tailNodes, 1);
        draw(pic, ende[(f-t+frames)%frames] -- ende[(f-t-1+frames)%frames], pene+linewidth);
        draw(pic, endb[(f-t+frames)%frames] -- endb[(f-t-1+frames)%frames], penb+linewidth);
      }
      draw(pic, (x, 0, 0) -- (epos), red, arrow=Arrow3(DefaultHead2(normal=Y)));
      draw(pic, (x, 0, 0) -- (bpos), blue, arrow=Arrow3(DefaultHead2(normal=Y)));
    }
  }
  draw(pic, ge, palered+linewidth(1pt));
  draw(pic, gb, paleblue+linewidth(1pt));
  label(pic, "$x$", (15.6, 0, 0));
  label(pic, "$y$", (0, 2.3, 0));
  label(pic, "$z$", (0, 0, 2.3));
  anime.add(pic);
}

label(anime.pdf("autoplay, loop", delay=50, keep=!settings.inlinetex));
\end{asy}
%Uncomment the following line when not using the [inline] package option:
%\ASYanimategraphics[autoplay,loop]{50}{movie3}{}{}
\end{center} 
\end{document}

